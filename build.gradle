import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id "com.github.hierynomus.license" version "0.14.0"
  id "com.xebialabs.xldp" version "1.0.5"
  id "com.xebialabs.xl.docker" version "1.1.0"
  id "nebula.release" version "6.0.0"
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

xlDocker {
  compileImage = 'xebialabs/xld_dev_compile'
  compileVersion = 'v7.2.0.1'
  runImage = 'xebialabs/xld_dev_run'
  runVersion = 'v7.2.0.1'
  runPortMapping = '14516:4516'
}

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}

def apiVersion = '2017.6.0'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://dist.xebialabs.com/public/maven2'
  }
}

configurations {
  compile {
    transitive = false
  }
}

dependencies {
  compile "org.scala-lang:scala-library:2.12.3"
  compile "com.xebialabs.deployit:udm-plugin-api:$apiVersion"
  compile "com.xebialabs.deployit:engine-spi:$apiVersion"
  compile "com.xebialabs.deployit:engine-api:$apiVersion"

  testCompile "org.clapper:grizzled-slf4j_2.12:1.3.0"
  testCompile "org.slf4j:slf4j-api:1.7.25"
  testCompile "com.google.guava:guava:19.0"
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}

processResources.configure {
  filter ReplaceTokens, tokens: [
    'project.version': version.toString(),
    'project.name'   : rootProject.name
  ]
}

license {
  header rootProject.file('License.md')
  strictCheck false
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
}
